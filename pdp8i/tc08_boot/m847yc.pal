/MI8-EC (TC08 DECtape)
DTLA=6766	/ LOAD REGISTER A
DTSF=6771	/ SKIP ON FLAGS
DTLB=6774	/ LOAD REGISTER B
	FIELD	0
	*7554
START,	7600		/ CLA; ALSO LOADING ADDRESS, AND COUNT
	DTLB		/ TRANSFER TO FIELD 0
	TAD MOVE	/ GET A MOVE COMMAND
	DTLA		/ MOVE
	DTSF		/ WAIT FOR IT
	JMP .-1
	CLA CMA 	/ GET LOADING ADDRESS - 1
	TAD START
	DCA I IDCA	/ SET CA
	TAD START	/ GET COUNT
	DCA I IDWC	/ SET WC
	TAD READ	/ GET A READ COMMAND
	DTLA		/ READ
	JMP WAIT	/ GO WAIT
IDWC,	7754		/ PTR TO WC
IDCA,	7755		/ PTR TO CA
MOVE,	0600		/ MOVE, NORMAL, GO
READ,	0220		/ READ, GO
WAIT,	DTSF		/ WAIT FOR THE READ
	JMP .-1
/ WHEN THE READ IS FINISHED, FALL THROUGH ONTO THE NEWLY LOADED CODE.
	*7600
	7402		/ REPLACED BY THE LOADED CODE.
	7777		/ (ALL DIODES REMOVED)
	7777
	7777
	7777
	7777
	7777
	7777
	7777
	7777
	7777
	7777
$
